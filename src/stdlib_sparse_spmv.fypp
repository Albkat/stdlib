#:include "common.fypp"
#:set RANKS = range(1, 2+1)
#:set R_KINDS_TYPES = list(zip(REAL_KINDS, REAL_TYPES, REAL_SUFFIX))
#:set C_KINDS_TYPES = list(zip(CMPLX_KINDS, CMPLX_TYPES, CMPLX_SUFFIX))
#:set KINDS_TYPES = R_KINDS_TYPES+C_KINDS_TYPES
#! define ranks without parentheses
#:def rksfx2(rank)
#{if rank > 0}#${":," + ":," * (rank - 1)}$#{endif}#
#:enddef
!> The `stdlib_sparse_spmv` module provides matrix-vector product kernels.
!>
! This code was modified from https://github.com/jalvesz/FSPARSE by its author: Alves Jose
module stdlib_sparse_spmv
    use stdlib_kinds, only: int8, int16, int32, int64, sp, dp, xdp, qp
    use stdlib_sparse_kinds
    implicit none
    private

    public :: spmv
    interface spmv
    !! Version experimental
    !!
    !! Applay the sparse matrix-vector product $$y = \beta * y + \alpha * M * x $$
    !!
        #:for k1, t1, s1 in (KINDS_TYPES)
        #:for rank in RANKS
        module procedure spmv_coo_${rank}$d_${s1}$
        module procedure spmv_csr_${rank}$d_${s1}$
        module procedure spmv_csc_${rank}$d_${s1}$
        module procedure spmv_ell_${rank}$d_${s1}$
        #:endfor
        module procedure spmv_sellc_${s1}$
        #:endfor
    end interface

contains

    !> spmv_coo
    #:for k1, t1, s1 in (KINDS_TYPES)
    #:for rank in RANKS
    subroutine spmv_coo_${rank}$d_${s1}$(matrix,vec_x,vec_y,alpha,beta)
        type(COO_${s1}$), intent(in) :: matrix
        ${t1}$, intent(in)    :: vec_x${ranksuffix(rank)}$
        ${t1}$, intent(inout) :: vec_y${ranksuffix(rank)}$
        ${t1}$, intent(in), optional :: alpha
        ${t1}$, intent(in), optional :: beta
        ${t1}$ :: alpha_, beta_
        integer :: k, ik, jk

        alpha_ = one_${k1}$
        if(present(alpha)) alpha_ = alpha
        if(present(beta)) then
            vec_y = beta * vec_y
        else 
            vec_y = zero_${s1}$
        endif
        associate( data => matrix%data, index => matrix%index, storage => matrix%storage, nnz => matrix%nnz )
            if( storage == sparse_full) then
                do concurrent (k = 1:nnz)
                    ik = index(1,k)
                    jk = index(2,k)
                    vec_y(${rksfx2(rank-1)}$ik) = vec_y(${rksfx2(rank-1)}$ik) + alpha_*data(k) * vec_x(${rksfx2(rank-1)}$jk)
                end do

            else 
                do concurrent (k = 1:nnz)
                    ik = index(1,k)
                    jk = index(2,k)
                    vec_y(${rksfx2(rank-1)}$ik) = vec_y(${rksfx2(rank-1)}$ik) + alpha_*data(k) * vec_x(${rksfx2(rank-1)}$jk)
                    if( ik==jk ) cycle
                    vec_y(${rksfx2(rank-1)}$jk) = vec_y(${rksfx2(rank-1)}$jk) + alpha_*data(k) * vec_x(${rksfx2(rank-1)}$ik)
                end do

            end if
        end associate
    end subroutine

    #:endfor
    #:endfor

    !! spmv_csr
    #:for k1, t1, s1 in (KINDS_TYPES)
    #:for rank in RANKS
    subroutine spmv_csr_${rank}$d_${s1}$(matrix,vec_x,vec_y,alpha,beta)
        type(CSR_${s1}$), intent(in) :: matrix
        ${t1}$, intent(in)    :: vec_x${ranksuffix(rank)}$
        ${t1}$, intent(inout) :: vec_y${ranksuffix(rank)}$
        ${t1}$, intent(in), optional :: alpha
        ${t1}$, intent(in), optional :: beta
        ${t1}$ :: alpha_, beta_
        integer :: i, j
        #:if rank == 1
        ${t1}$ :: aux, aux2
        #:else
        ${t1}$ :: aux(size(vec_x,dim=1)), aux2(size(vec_x,dim=1))
        #:endif
        
        alpha_ = one_${k1}$
        if(present(alpha)) alpha_ = alpha
        beta_ = zero_${k1}$
        if(present(beta)) beta_ = beta

        associate( data => matrix%data, col => matrix%col, rowptr => matrix%rowptr, &
            & nnz => matrix%nnz, nrows => matrix%nrows, ncols => matrix%ncols, storage => matrix%storage )
            if( storage == sparse_full) then
                do i = 1, nrows
                    aux = zero_${k1}$
                    do j = rowptr(i), rowptr(i+1)-1
                        aux = aux + data(j) * vec_x(${rksfx2(rank-1)}$col(j))
                    end do
                    if(present(beta)) then
                        vec_y(${rksfx2(rank-1)}$i) = beta_ * vec_y(${rksfx2(rank-1)}$i) + alpha_ * aux
                    else
                        vec_y(${rksfx2(rank-1)}$i) = alpha_ * aux
                    end if
                end do
                
            else if( storage == sparse_lower )then
                do i = 1 , nrows
                    aux  = zero_${s1}$
                    aux2 = alpha_ * vec_x(${rksfx2(rank-1)}$i)
                    do j = rowptr(i), rowptr(i+1)-2
                        aux = aux + data(j) * vec_x(${rksfx2(rank-1)}$col(j))
                        vec_y(${rksfx2(rank-1)}$col(j)) = vec_y(${rksfx2(rank-1)}$col(j)) + data(j) * aux2
                    end do
                    aux = alpha_ * aux + data(j) * aux2

                    if(present(beta)) then
                        vec_y(${rksfx2(rank-1)}$i) = beta_ * vec_y(${rksfx2(rank-1)}$i) + aux
                    else
                        vec_y(${rksfx2(rank-1)}$i) = aux
                    end if
                end do

            else if( storage == sparse_upper )then
                do i = 1 , nrows
                    aux  = vec_x(${rksfx2(rank-1)}$i) * data(rowptr(i))
                    aux2 = alpha_ * vec_x(${rksfx2(rank-1)}$i)
                    do j = rowptr(i)+1, rowptr(i+1)-1
                        aux = aux + data(j) * vec_x(${rksfx2(rank-1)}$col(j))
                    end do
                    if(present(beta)) then
                        do j = rowptr(i)+1, rowptr(i+1)-1
                            vec_y(${rksfx2(rank-1)}$col(j)) = beta_ * vec_y(${rksfx2(rank-1)}$col(j)) + data(j) * aux2
                        end do
                    else
                        do j = rowptr(i)+1, rowptr(i+1)-1
                            vec_y(${rksfx2(rank-1)}$col(j)) = data(j) * aux2
                        end do
                    end if
                    vec_y(${rksfx2(rank-1)}$i) = vec_y(${rksfx2(rank-1)}$i) + alpha_ * aux
                end do

            end if
        end associate
    end subroutine
    
    #:endfor
    #:endfor

    !> spmv_csc
    #:for k1, t1, s1 in (KINDS_TYPES)
    #:for rank in RANKS
    subroutine spmv_csc_${rank}$d_${s1}$(matrix,vec_x,vec_y,alpha,beta)
        type(CSC_${s1}$), intent(in) :: matrix
        ${t1}$, intent(in)    :: vec_x${ranksuffix(rank)}$
        ${t1}$, intent(inout) :: vec_y${ranksuffix(rank)}$
        ${t1}$, intent(in), optional :: alpha
        ${t1}$, intent(in), optional :: beta
        ${t1}$ :: alpha_, beta_
        integer :: i, j
        #:if rank == 1
        ${t1}$ :: aux
        #:else
        ${t1}$ :: aux(size(vec_x,dim=1))
        #:endif

        alpha_ = one_${k1}$
        if(present(alpha)) alpha_ = alpha
        if(present(beta)) then
            vec_y = beta * vec_y
        else 
            vec_y = zero_${s1}$
        endif

        associate( data => matrix%data, colptr => matrix%colptr, row => matrix%row, &
            & nnz => matrix%nnz, nrows => matrix%nrows, ncols => matrix%ncols, storage => matrix%storage )
            if( storage == sparse_full) then
                do concurrent(j=1:ncols)
                    do i = colptr(j), colptr(j+1)-1
                        vec_y(${rksfx2(rank-1)}$row(i)) = vec_y(${rksfx2(rank-1)}$row(i)) + alpha_ * data(i) * vec_x(${rksfx2(rank-1)}$j)
                    end do
                end do

            else if( storage == sparse_lower )then
                ! NOT TESTED
                do j = 1 , ncols
                    aux  = vec_x(${rksfx2(rank-1)}$j) * data(colptr(j))
                    do i = colptr(j)+1, colptr(j+1)-1
                        aux = aux + data(i) * vec_x(${rksfx2(rank-1)}$row(i))
                        vec_y(${rksfx2(rank-1)}$row(i)) = vec_y(${rksfx2(rank-1)}$row(i)) + alpha_ * data(i) * vec_x(${rksfx2(rank-1)}$j)
                    end do
                    vec_y(${rksfx2(rank-1)}$j) = vec_y(${rksfx2(rank-1)}$j) + alpha_ * aux
                end do

            else if( storage == sparse_upper )then
                ! NOT TESTED
                do j = 1 , ncols
                    aux  = zero_${s1}$
                    do i = colptr(j), colptr(i+1)-2
                        aux = aux + data(i) * vec_x(${rksfx2(rank-1)}$j)
                        vec_y(${rksfx2(rank-1)}$i) = vec_y(${rksfx2(rank-1)}$i) + alpha_ * data(i) * vec_x(${rksfx2(rank-1)}$row(i))
                    end do
                    aux = aux + data(colptr(j)) * vec_x(${rksfx2(rank-1)}$j)
                    vec_y(${rksfx2(rank-1)}$j) = vec_y(${rksfx2(rank-1)}$j) + alpha_ * aux
                end do
                
            end if
        end associate
    end subroutine
    
    #:endfor
    #:endfor

    !> spmv_ell
    #:for k1, t1, s1 in (KINDS_TYPES)
    #:for rank in RANKS
    subroutine spmv_ell_${rank}$d_${s1}$(matrix,vec_x,vec_y,alpha,beta)
        type(ELL_${s1}$), intent(in) :: matrix
        ${t1}$, intent(in)    :: vec_x${ranksuffix(rank)}$
        ${t1}$, intent(inout) :: vec_y${ranksuffix(rank)}$
        ${t1}$, intent(in), optional :: alpha
        ${t1}$, intent(in), optional :: beta
        ${t1}$ :: alpha_, beta_
        integer :: i, j, k

        alpha_ = one_${k1}$
        if(present(alpha)) alpha_ = alpha
        if(present(beta)) then
            vec_y = beta * vec_y
        else 
            vec_y = zero_${s1}$
        endif
        associate( data => matrix%data, index => matrix%index, MNZ_P_ROW => matrix%K, &
            & nnz => matrix%nnz, nrows => matrix%nrows, ncols => matrix%ncols, storage => matrix%storage )
            if( storage == sparse_full) then
                do concurrent (i = 1:nrows, k = 1:MNZ_P_ROW)
                    j = index(i,k)
                    if(j>0) vec_y(${rksfx2(rank-1)}$i) = vec_y(${rksfx2(rank-1)}$i) + alpha_*data(i,k) * vec_x(${rksfx2(rank-1)}$j)
                end do
                
            end if
        end associate
    end subroutine
    
    #:endfor
    #:endfor

    !> spmv_sellc
    #:set CHUNKS = [4,8,16]
    #:for k1, t1, s1 in (KINDS_TYPES)
    subroutine spmv_sellc_${s1}$(matrix,vec_x,vec_y,alpha,beta)
        !> This algorithm was gracefully provided by Ivan Privec and adapted by Jose Alves
        type(SELLC_${s1}$), intent(in) :: matrix
        ${t1}$, intent(in)    :: vec_x(:)
        ${t1}$, intent(inout) :: vec_y(:)
        ${t1}$, intent(in), optional :: alpha
        ${t1}$, intent(in), optional :: beta
        ${t1}$ :: alpha_, beta_
        integer :: i, nz, rowidx, num_chunks, rm

        alpha_ = one_${s1}$
        if(present(alpha)) alpha_ = alpha
        if(present(beta)) then
            vec_y = beta * vec_y
        else 
            vec_y = zero_${s1}$
        endif
        associate( data => matrix%data, ia => matrix%rowptr , ja => matrix%col, cs => matrix%chunk_size, &
        &   nnz => matrix%nnz, nrows => matrix%nrows, ncols => matrix%ncols, storage => matrix%storage  )
        num_chunks = nrows / cs
        rm = nrows - num_chunks * cs
        if( storage == sparse_full) then

            select case(cs)
            #:for chunk in CHUNKS
            case(${chunk}$)
                do i = 1, num_chunks
                    nz = ia(i+1) - ia(i)
                    rowidx = (i - 1)*${chunk}$ + 1 
                    call chunk_kernel_${chunk}$(nz,data(:,ia(i)),ja(:,ia(i)),vec_x,vec_y(rowidx:))
                end do
            #:endfor
            case default
                print *, "error: chunk size not supported."
                return
            end select
            
            ! remainder
            if(rm>0)then 
                i = num_chunks + 1 
                nz = ia(i+1) - ia(i)
                rowidx = (i - 1)*cs + 1
                call chunk_kernel_remainder(nz,cs,rm,data(:,ia(i)),ja(:,ia(i)),vec_x,vec_y(rowidx:))
            end if

        end if
        end associate

    contains
    #:for chunk in CHUNKS
    pure subroutine chunk_kernel_${chunk}$(nz,a,ja,x,y)
        integer, value      :: nz
        ${t1}$, intent(in)  :: a(${chunk}$,nz), x(*)
        integer, intent(in) :: ja(${chunk}$,nz)
        ${t1}$, intent(inout) :: y(${chunk}$)
        integer :: j
        do j = 1, nz
            where(ja(:,j) > 0) y = y + alpha_ * a(:,j) * x(ja(:,j))               
        end do
    end subroutine
    #:endfor

    pure subroutine chunk_kernel_remainder(nz,cs,rm,a,ja,x,y)
        integer, value      :: nz, cs, rm
        ${t1}$, intent(in)  :: a(cs,nz), x(*)
        integer, intent(in) :: ja(cs,nz)
        ${t1}$, intent(inout) :: y(rm)
        integer :: j
        do j = 1, nz
            where(ja(1:rm,j) > 0) y = y + alpha_ * a(1:rm,j) * x(ja(1:rm,j))               
        end do
    end subroutine  

    end subroutine
    
    #:endfor
    
end module stdlib_sparse_spmv